load("util.stlx");
load("ops.stlx");
load("variables.stlx");


idxOfClosedParan := procedure(tokens) {
	if (global.DEBUG) {
		print("Finding index of closed parantheses $tokens$...");
	}
	parans := 1;
	i := 1;
	while (parans != 0 && i <= #tokens) {
		match(tokens[i]) {
			case @TokParanOpen():
				parans += 1;
			case @TokParanClosed():
				parans -= 1;
		}
		i += 1;
	}

	if (parans == 0) {
		return i-1;
	} else {
		abort("Error at idxOfClosedParan($tokens$): Unmatched Parantheses.");
	}
};

parse := procedure(tokens, rw varTable) {
	if (isString(tokens)) {
		tokens := tokenize(tokens);
	}
	xs := splitBy(tokens, @TokWhere());
	exprTokens := xs[1];
	for (toks in reverse(xs[2..])) {
		parseAssignment(toks, varTable);
	}
	return parseExpr(xs[1], varTable);
};

parseAssignment := procedure(tokens, rw varTable) {
	assert(fct(tokens[1]) == "TokName", "The first token in an assignment must be a name, instead received $tokens[1]$");
	assert(tokens[2] == @TokEqual(), "The second token in an assignment must be an equal-sign, instead received $tokens[2]$");

	var := args(tokens[1])[1];
	if (varTable.isDefinedVar(var)) {
		abort("Error at parseAssignment($tokens$) - $var$ is already defined. You can't overwrite variable definitions.");
	}
	if (@TokName(var) in tokens[3..]) {
		abort("Error at parseAssignment($tokens$) - Recursive definitions are not allowed.");
	}

	expr := parseExpr(tokens[3..], varTable);
	varTable.addVar(var, expr);
};

parseExprList := procedure(tokens, rw varTable) {
	if (global.DEBUG) {
		print("Parsing expression-list $tokens$...");
	}

	exprs := [];
	parans := 1;
	i := 1;
	while (parans != 0 && #tokens >= i) {
		match(tokens[i]) {
			case @TokParanOpen():
				parans += 1;
			case @TokParanClosed():
				parans -= 1;
			case @TokComma():
				if (parans == 1) {
					exprs += [tokens[..i-1]];
					tokens := tokens[i+1..];
					i := 0;
				}
		}
		i += 1;
	}

	if (parans == 0) {
		exprs += [tokens[..-2]];
		exprs := [ parseExpr(x, varTable) : x in exprs ];
		return exprs;
	} else {
		abort("Error at parseExprList($tokens$): Unmatched Parantheses.");
	}
};

parseExpr := procedure(tokens, rw varTable, last := om, lastPrec := om, paranLevel := 0) {
	if (global.DEBUG) {
		print("Parsing expression $tokens$ with last: $last$...");
	}
	if (tokens == []) {
		return last;
	}
	if (last == om && fct(tokens[1]) != "TokOp") {
		if (global.DEBUG) {
			print("last is om && first token is not op: $tokens[1]$");
		}
		term := parseTerms(tokens, varTable, paranLevel);
		return parseExpr(tokens, varTable, term, om, 0);
	}

	expr := om;
	token := tokens[1];
	tokens := tokens[2..];
	currPrec := om;
	match(token) {
		case @TokOp(op):
			assert(tokens != [], "An operator must be followed by an expression");
			next := parseTerms(tokens, varTable, paranLevel);
			currPrec := getOpPrec(op) + paranLevel * getMaxPrec();
			if (global.DEBUG) {
				print("next: $next$");
				print("lastPrec: $lastPrec$");
				print("currPrec: $currPrec$");
				print("last: $last$");
			}
			if (last == om) {
				expr := @UnaryOp(op, next);
			} else if (fct(last) == "Op") {
				[lastOp, params] := args(last);
				if (lastPrec == om) {
					// Only happens if the last thing was a list of terms (i.e. an immediate multiplication)
					lastPrec := getOpPrec("*") + paranLevel * getMaxPrec();
				}
				assert(#params >= 2, "There must be at least 2 parameters for a prefix operator. The operator $lastOp$ only received the parameters $params$");

				if (op == lastOp && isOpAssoc(op)) {
					expr := @Op(op, params + [next]);
				} else if (op == lastOp && isOpRightAssoc(op)) {
					assert(#params == 2, "Right-Associative operations, that aren't also left-associative are assumed to always take exactly 2 operands.");
					expr := @Op(op, [params[1], @Op(op, [params[2], next])]);
				} else if (lastPrec < currPrec) {
					expr := @Op(lastOp, params[..-2] + [@Op(op, [params[-1], next])]);
					currPrec := lastPrec;
				} else {
					expr := @Op(op, [last, next]);
				}
			} else {
				expr := @Op(op, [last, next]);
			}
		// TODO: This check is basically repeated in parseTerms, because parseTerms currently doesn't take numbers in implicit multiplications into account
		case t | fct(last) in ["Name", "Number"] && fct(token) in ["TokName", "TokNum"]:
			curr := parseTerm([token], varTable, 0);
			currPrec := getOpPrec("*") + paranLevel * getMaxPrec();
			expr := @Op("*", [curr, last]);
		default:
			if (global.DEBUG) {
				stop("parseExpr - default case:");
			}
			abort("Error at parseExpr($[token] + tokens$, $last$) - something went wrong, because this place should unreachable.");
	}
	if (global.DEBUG) {
		print("Parsing done - Result: $expr$");
	}
	return parseExpr(tokens, varTable, expr, currPrec, paranLevel);
};

parseTerms := procedure(rw tokens, varTable, paranLevel := 0) {
	if (global.DEBUG) {
		print("Parsing repeated terms $tokens$...");
	}
	terms := [parseTerm(tokens, varTable, paranLevel)];
	while(tokens != []) {
		toksClone := tokens;
		try {
			next := parseTerm(tokens, varTable, paranLevel);
			terms += [next];
		} catch(e) {
			tokens := toksClone;
			break;
		}
	}
	if (#terms > 1) {
		return @Op("*", terms);
	} else {
		return terms[1];
	}
};

// TODO: Return precedence of operation (when variable is substituted)
parseTerm := procedure(rw tokens, varTable, paranLevel := 0) {
	assert(tokens != [], "parseTerm expects a non-empty list as input");
	if (global.DEBUG) {
		print("Parsing term $tokens$...");
	}
	match(tokens[1]) {
		case @TokNum(num):
			tokens := tokens[2..];
			return @Number(num);
		case @TokName(name):
			res := @Name(name);
			prec := om;
			tokens := tokens[2..];
			isFollowedByParans := #tokens > 2 && fct(tokens[1]) == "TokParanOpen";

			if (varTable.isDefinedVar(name)) {
				res := varTable.getDefinedVar(name);
				if (fct(res) == "Op") {
					prec := getOpPrec(args(res)[1]) + getMaxPrec() * (paranLevel + 1);
				}
			} else if (name in varTable.funcs) {
				assert(isFollowedByParans, "$name$ is a function and must be followed by parantheses");
			} else if (!varTable.isUndefinedVar(name) && !varTable.isCategorizedVar(name)) {
				varTable.addVar(name);
			}
			// Name + ParanOpen + ParanClosed are required for it to be a function-call
			if (isFollowedByParans) {
				tokens := tokens[2..];
				idx := idxOfClosedParan(tokens);
				assert(fct(tokens[idx]) == "TokParanClosed", "The index returned by idxOfClosedParan should point to a closing parantheses. Returned index is $idx$ and points at $tokens[idx]$");
				innerArgs := tokens[..idx];
				tokens := tokens[idx+1..];
				params := parseExprList(innerArgs, varTable);
				if (name in varTable.funcs) {
					res := @Func(name, params);
				} else if (#params > 1) {
					abort("The variable $name$ is not a valid function. `$name$($join(params, ', ')$)` is a function call, because there are several arguments separated by ','");
				} else {
					innerExpr := params[1];
					prec := getOpPrec("*") + getMaxPrec() * paranLevel;
					res := @Op("*", [res, innerExpr]);
				}
			}
			return res;
		case @TokParanOpen():
			tokens := tokens[2..];
			idx := idxOfClosedParan(tokens);
			expr := parseExpr(tokens[..idx-1], varTable, om, om, paranLevel + 1);
			lastPrec := om;
			if (fct(expr) == "Op") {
				lastPrec := getOpPrec(args(expr)[1]) + getMaxPrec() * (paranLevel + 1);
			}
			tokens := tokens[idx+1..];
			return expr;
		default: abort("Error at parseTerm($tokens$) - Unexpected token");
	}
};

tokenize := procedure(str) {
	if (global.DEBUG) {
		print("Tokenizing $str$...");
	}
	toks := [];
	scan(str) {
		regex '\s+': // Skip starting whitespace
		regex 'where\s+':
			toks += [ @TokWhere() ];
		regex '=':
			toks += [ @TokEqual() ];
		regex '[a-z]|[A-Z][a-zA-Z_0-9]*' as [ name ]:
			toks += [ @TokName(name) ];
		regex '(0|[1-9][0-9]*).[0-9]+' as [ num, _ ]:
			toks += [ @TokNum(double(num)) ];
		regex '0|[1-9][0-9]*' as [ num ]:
			toks += [ @TokNum(int(num)) ];
		regex '\(':
			toks += [ @TokParanOpen() ];
		regex '\)':
			toks += [ @TokParanClosed() ];
		regex ',':
			toks += [ @TokComma() ];
		regex '[\^+\-\*/]' as [ op ]:
			toks += [ @TokOp(op) ];
		default: abort("Error at tokenize($str$) - Can't tokenize $str$ properly");
	}
	return toks;
};

stringify := procedure(expr, pretty := true, outsidePrec := om, isFirstOperand := false) {
	if (global.DEBUG) {
		print("Stringifying $expr$ with outsideOp = $outsideOp$...");
	}
	match(expr) {
		case @Name(s):
			return s;
		case @Number(n):
			return "$n$";
		case @Func(f, params):
			params := [ stringify(p, pretty) : p in params ];
			params := join(params);
			return "$f$($params$)";
		case @Op(op, params):
			prec := getOpPrec(op);
			res := om;
			opStr := " $op$ ";
			if (op == "^") {
				opStr := "^";
			}

			if (pretty && op == "*" && exists (p in params | fct(p) == "Name")) {
				[names, nums, others] := mappedSplitList(params, closure(p, i) {
					if (fct(p) == "Name") {
						return [1, stringify(p, pretty)];
					} else if (fct(p) == "Number") {
						return [2, stringify(p, pretty)];
					} else {
						return [3, stringify(p, pretty, prec, i == 1)];
					}
				}, 3);
				assert(#nums <= 1, "A product shouldn't have more than 1 number after being evaluated");
				res := join(nums + names, "");
				res := join([res] + others, opStr);
			} else {
				params := map(params, [p, i] |=> stringify(p, pretty, prec, i == 1));
				res := join(params, opStr);
			}

			if (isInteger(outsidePrec) && (outsidePrec > prec || (outsidePrec == prec && !isFirstOperand))) {
				return "($res$)";
			} else {
				return res;
			}
		default: abort("Error at stringify($expr$, $outsideOp$) - Unexpected expression.");
	}
};