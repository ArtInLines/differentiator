load("util.stlx");


addOpRule := procedure(rw rules, op, test, eval) {
	if (op in domain(rules)) {
		rules[op] += [[test, eval]];
	} else {
		rules[op] := [[test, eval]];
	}
	return rules;
};

addOpRules := procedure(rw rules, op, newRules) {
	for (newRule in newRules) {
		assert(#newRule == 2, "An evalutation rule always has to consist of exactly two functions, a test and an eval function.");
		addOpRule(rules, op, newRule[1], newRule[2]);
	}
	return rules;
};

class cachedExprRuleClass(op, calcCacheVal, replace) {
	op := op;
	calcCacheVal := calcCacheVal;
	replace := replace;
	cachedVal := om;
	test := procedure(params) {
		cachedExprs := {};
		for (p in params) {
			val := this.calcCacheVal(p);
			if (val != om) {
				if (val in cachedExprs) {
					this.cachedVal := val;
					return true;
				}
				cachedExprs += {val};
			}
		}
		return false;
	};
	eval := procedure(params) {
		assert(this.cachedVal != om, "Cached Value shouldn't be able to be om, since values that are om should get discarded.");
		[ sames, others ] := splitListInTwo(params, p |=> cachedVal == calcCacheVal(p));
		replacement := this.replace(sames);
		if (others == []) {
			return replacement;
		} else {
			return @Op(this.op, others + [replacement]);
		}
	};
}

// op is the operation combining the other values with the replacement of the matched value (usually the same operation as the one for which the rule is defined)
// calcCacheVal is a function taking a single expression as input. It should return a cache value for comparing with the cache values of the other parameters in params. Return `om` if you want to ignore said expression. If there are two or more expression with the same cache value, the rule applies.
// replace is a function taking the list of expressions, that were matched as same with calcCacheVal. It should return an expression to use in their stead. If all expressions matched it, the replacement will be returned directly. Otherwise it will be connected with the other expression using the operator from `op`.
cachedExprRule := closure(op, calcCacheVal, replace) {
	r := cachedExprRuleClass(op, calcCacheVal, replace);
	return [params |=> r.test(params), params |=> r.eval(params)];
};

someSameExprsRule := procedure(op, replace) {
	return cachedExprRule(op, p |-> p, replace);
};

allNumRule := procedure(reduceOp, reversed := false) {
	return [
		params |-> forall (p in params | fct(p) == "Number"),
		params |=> @Number(reduceFromFirst([ args(p)[1] : p in params ], reduceOp, reversed))
	];
};

// Assumes the operation to be commutative (at least for elements after the firstN)
someNumRule := procedure(op, reduceOp, reversed := false, skipFirstN := 0) {
	return [
		params |=> some(params[1 + skipFirstN..], p |-> fct(p) == "Number"),
		closure(params) {
			[ nums, others ] := splitListInTwo(params[1 + skipFirstN..], p |-> fct(p) == "Number");
			others := params[1..skipFirstN] + others;
			n := reduceFromFirst([ args(n)[1] : n in nums ], reduceOp, reversed);
			if (others == []) {
				return @Number(n);
			} else {
				return @Op(op, others + [@Number(n)]);
			}
		}
	];
};

associativeRule := procedure(op, termFct := "Op") {
	return [
		params |=> exists (p in params | fct(p) == termFct && args(p)[1] == op),
		params |=> makeTerm(termFct, [op, flatten(map(params, closure(p, i) {
			if (fct(p) == termFct && args(p)[1] == op) {
				return args(p)[2..];
			} else {
				return p;
			}
		}))])
	];
};

neutralElRule := procedure(op, e, skipFirstN := 0) {
	return [
		params |=> exists (p in params[1 + skipFirstN..] | fct(p) == "Number" && args(p)[1] == e),
		closure(params) {
			params := params[1..skipFirstN] + [ p : p in params[1 + skipFirstN..] | fct(p) != "Number" || args(p)[1] != e ];
			if (#params == 1) {
				return params[1];
			} else {
				return @Op(op, params);
			}
		}
	];
};

// cmpType can be positive, negative or 0. If 0, the arity has to be equal to the provided one. If positive, the arity has to be greater than or equal. If negative, the arity has to be less than or equal.
checkArityRule := procedure(op, arity, cmpType := 0) {
	return [
		closure(params) {
			if (cmpType == 0) {
				return #params != arity;
			} else if (cmpType < 0) {
				return #params > arity;
			} else {
				return #params < arity;
			}
		},
		params |=> abort("$op$ exprects $arity$ argument(s), but received $#params$ argument(s).")
	];
};

chainRule := procedure(f, innerRules) {
	// Assume each rule in innerRules is a list with two elements, where the first element is the name of the function and the second element is a function taking the parameters as input and returning an expression as output
	return [
		params |=> fct(params[1]) == "Func" && args(params[1])[1] in domain(innerRules),
		closure(params) {
			fname := args(params[1])[1];
			fparams := args(params[1])[2];
			otherParams := params[2..];
			fdiff := innerRules[fname];
			return @Op("*", [@Func(f, fparams + otherParams), fdiff(fparams)]);
		}
	];
};

// Assumes that invOfNum(invOfNum(a)) == a
invSomeNumRule := procedure(op, invOp, reduceOp, invOfNum) {
	testExpr := p |=> fct(p) == "Number" || (fct(p) == "Op" && args(p)[1] in {op, invOp} && exists (x in args(p)[2] | testExpr(x)));
	test := params |=> testExpr(params[1]) && testExpr(params[2]);

	splitExpr := closure(p, op, invOp) {
		match(p) {
			case @Number(n):
				return [n, @Null()];
			case @Op(op, xs):
				assert(#xs == 2, "This rule assumes that the operation $op$ requires exactly 2 operands");
				[[a, b], [c, d]] := [splitExpr(xs[1], op, invOp), splitExpr(xs[2], invOp, op)];
				if (a != @Null() && b == @Null()) {
					if (c != @Null()) {
						return [reduceOp(a, c), d];
					} else {
						return [a, d];
					}
				} else if (a == @Null() && b != @Null()) {
					if (d == @Null()) {
						return [invOfNum(c), b];
					} else {
						return [invOfNum(c), @Op(op, [b, d])];
					}
				} else if (a != @Null() && b != @Null()) {
					if (c == @Null() && d != @Null()) {
						return [a, @Op(op, [b, d])];
					} else if (c != @Null() && d == @Null()) {
						return [reduceOp(a, c), b];
					} else if (c != @Null() && d != @Null()) {
						return [reduceOp(a, c), @Op(op, [b, d])];
					} else {
						assert(false, "Unreachable. For debugging: a=$a$, b=$b$, c=$c$, d=$d$");
					}
				} else {
					assert(false, "Unreachable. For debugging: a=$a$, b=$b$, c=$c$, d=$d$");
				}
			case @Op(invOp, xs):
				return splitExpr(@Op(op, xs), invOp, op);
			default:
				return [@Null(), p];
		}
	};
	eval := closure(params) {
		assert(#params == 2, "This rule assumes that the operation $op$ requires exactly 2 operands");
		[n, x] := splitExpr(@Op(op, params), op, invOp);
		if (x == @Null()) {
			return @Number(n);
		} else {
			return @Op(invOp, [x, @Number(n)]);
		}
	};
	return [test, eval];
};

makeRules := procedure(rules, singleRulePerOp := false) {
	res := {};
	for (r in rules) {
		assert(#r == 2, "Every rule must be a list with two elements.");
		if (singleRulePerOp) {
			res[r[1]] := r[2];
		} else {
			addOpRules(res, r[1], r[2]);
		}
	}
	return res;
};

makeDiffFuncRules := procedure() {
	return makeRules([
		["sin", params |-> @Func("cos", params)],
		["cos", params |-> @UnaryOp("-", @Func("sin", params))]
	], true);
};

makeDefaultRules := procedure(diffFuncRules := makeDiffFuncRules()) {
	return makeRules([
		["^", [
			// Must have exactly 2 operands
			checkArityRule("^", 2),
			// numbers can be directly calculated
			allNumRule([a,b] |-> a ** b),
			// a ^ 0 = 1
			[
				params |-> params[2] == @Number(0),
				params |-> @Number(1)
			],
			// a ^ 1 = a
			[
				params |-> params[2] == @Number(1),
				params |-> params[1]
			],
			// 0^a = 0 && 1^a = 1
			[
				params |-> params[1] in {@Number(0), @Number(1)},
				params |-> params[1]
			],
			// a^(b^c) = a^(b*c)
			[
				params |-> fct(params[2]) == "Op" && args(params[2])[1] == "^",
				params |-> @Op("^", [params[1], @Op("*", args(params[2])[2..])])
			]
		]],
		["+", [
			// Must have at least 2 operands
			checkArityRule("+", 2, 1),
			// (a + b) + c = a + (b + c)
			associativeRule("+", "Op"),
			// Commutative && numbers can be directly calculated
			someNumRule("+", [a, b] |-> a + b),
			// Commutative && a+a = 2*a
			someSameExprsRule("+", sames |-> @Op("*", [@Number(#sames), sames[1]])),
			// a + 0 + b = a + b
			neutralElRule("+", 0),
			// a + (-n) = a - n
			[
				params |-> exists (p in params | fct(p) == "Number" && args(p)[1] < 0),
				procedure(params) {
					[negs, pos] := splitListInTwo(params, p |-> fct(p) == "Number" && args(p)[1] < 0);
					negs := [@Number(-args(n)[1]) : n in negs];
					return @Op("-", [@Op("+", [@Number(0)] + pos), @Op("+", [@Number(0)] + negs)]);
				}
			],
			// a + (b - c) + (d - e) = (a + b + d) - (c + e)
			[
				params |-> exists (p in params | fct(p) == "Op" && args(p)[1] == "-"),
				procedure(params) {
					[negs, pos] := splitListInTwo(params, p |-> fct(p) == "Op" && args(p)[1] == "-");
					pos += [ args(p)[2][1] : p in negs ];
					negs := map(negs, [p, i] |-> args(p)[2][2]);
					if (#negs > 1) {
						return @Op("-", [@Op("+", pos), @Op("+", negs)]);
					} else {
						return @Op("-", [@Op("+", pos), negs[1]]);
					}
				}
			]
		]],
		["*", [
			// Must have at least 2 operands
			checkArityRule("*", 2, 1),
			// 0 * a = a * 0 = 0
			[
				params |-> exists (p in params | p == @Number(0)),
				params |-> @Number(0)
			],
			// a * (b * c) = (a * b) * c
			associativeRule("*", "Op"),
			// Commutative && numbers can be directly calculated
			someNumRule("*", [a, b] |-> a * b),
			// Commutative && a * a = a ^ 2
			someSameExprsRule("*", sames |-> @Op("^", [sames[1], @Number(#sames)])),
			// a * 1 = 1 * a = a
			neutralElRule("*", 1),
			// (a ^ b) * (a ^ c) = a ^ (b + c)
			cachedExprRule("*", procedure(p) {
				match(p) {
					case @Op(op, params):
						if (op == "^") {
							return params[1];
						} else {
							return om;
						}
					case @Func(f, params):
						return om;
					case @Name(x):
						return x;
					case @Number(x):
						return x;
				}
			}, procedure(sames) {
				// whether "^" operation, name or number, the base is always given by the first argument
				base := args(sames[1])[1];
				exps := [];
				for (x in sames) {
					if (fct(x) == "Op") {
						l := args(x)[2..];
						if (#l == 1) {
							exps += l;
						} else {
							exps += [@Op("*", l)];
						}
					} else {
						exps += [@Number(1)];
					}
				}
				return @Op("^", [base, @Op("+", exps)]);
			}),
			// a * b * (c +/- d)  =  a * b * c  +/-  a * b * d
			// TODO: Currently only applies if there is exactly one paranthesized expression - is this wanted?
			// TODO: Port rule to "/" (or generalize into an abstract rule?)
			[
				params |-> #[p : p in params | fct(p) == "Op" && args(p)[1] in {"+", "-"}] == 1 && exists ( p in params | fct(p) != "Op" || args(p)[1] notin {"+", "-"} ),
				procedure(params) {
					[distributables, others] := splitListInTwo(params, p |-> fct(p) == "Op" && args(p)[1] in {"+", "-"});

					assert(#distributables == 1, "Rule should currently only apply if there is exactly one paranthesized expression to distribute");
					[distOp, distParams] := args(distributables[1]);
					return @Op(distOp, [ @Op("*", others + [dp]) : dp in distParams ]);
				}
			],
			// a * (b / c) = (a * b) / c  &&  (a / b) * c = (a * c) / b
			[
				params |-> exists (p in params | fct(p) == "Op" && args(p)[1] == "/"),
				procedure(params) {
					[xs, ys] := splitListInTwo(params, p |-> fct(p) != "Op" || args(p)[1] != "/");
					nums := xs + [ args(y)[2][1] : y in ys ];
					dens := [ args(y)[2][2] : y in ys ];
					// stop("test");
					return @Op("/", [@Op("*", [@Number(1)] + nums), @Op("*", [@Number(1)] + dens)]);
				}
			]
		]],
		["-", [
			// If used as unary operation (negation)
			// All other rules don't take unary negation into effect, so this rule must not return a unary negation
			[
				params |-> #params == 1,
				procedure(params) {
					p := params[1];
					match(p) {
						case @Number(n):
							return @Number(-n);
						case @UnaryOp(op, arg):
							assert(arg == "-", "Negation is currently assumed to be the only unary operation");
							return arg;
						case @Op("-", args):
							return @Op("+", [@UnaryOp("-", args[1]), args[2]]);
						case @Op("+", args):
							return @Op("-", [@Number(0)] + args);
						default:
							return @Op("-", [@Number(0), p]);
					}
				}
			],
			// Must have exactly two operands
			checkArityRule("-", 2),
			// numbers can be directly calculated
			allNumRule([a, b] |-> a - b),
			// a - 0 = a
			[
				params |-> params[2] == @Number(0),
				params |-> params[1]
			],
			// a - a = 0
			[
				params |-> params[1] == params[2],
				params |-> @Number(0)
			],
			// Numbers can be calculated directly
			invSomeNumRule("-", "+", [a,b] |-> a-b, a |-> -a),
			// a - (b - c) = (a + c) - b
			[
				params |-> fct(params[2]) == "Op" && args(params[2])[1] == "-",
				procedure(params) {
					[a, [b, c]] := [params[1], args(params[2])[2]];
					return @Op("-", [@Op("+", [a, c]), b]);
				}
			],
			// (a - b) - c = a - (b + c)
			[
				params |-> fct(params[2]) == "Op" && args(params[2])[1] == "-",
				procedure(params) {
					[[a, b], c] := [args(params[1])[2], params[2]];
					return @Op("-", [a, @Op("+", [b, c])]);
				}
			]
		]],
		["/", [
			// Must have exactly 2 operands
			checkArityRule("/", 2),
			// a / 0 is not allowed
			[
				params |-> params[2] == @Number(0),
				params |-> abort("Division by 0")
			],
			// Numbers can be calculated directly
			invSomeNumRule("/", "*", [a,b] |-> a/b, a |-> 1/a),
			// 0 / a == 0
			[
				params |-> params[1] == @Number(0),
				params |-> @Number(0)
			],
			// a / 1 == a
			[
				params |-> params[2] == @Number(1),
				params |-> params[1]
			],
			// (a / b) / c == a / (b * c)
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "/",
				procedure(params) {
					[[a, b], c] := [args(params[1])[2], params[2]];
					return @Op("/", [a, @Op("*", [b, c])]);
				}
			],
			// a / (b / c) == (a * c) / b
			[
				params |-> fct(params[2]) == "Op" && args(params[2])[1] == "/",
				procedure(params) {
					[a, [b, c]] := [params[1], args(params[2])[2]];
					return @Op("/", [@Op("*", [a, c]), b]);
				}
			]
		]],
		["diff", [
			// Must have exactly two arguments
			checkArityRule("diff", 2),
			// derivative of a number is 0
			[
				params |-> fct(params[1]) == "Number",
				params |-> @Number(0)
			],
			// derivative of x by x is 1
			[
				params |-> fct(params[1]) == "Name" && params[1] == params[2],
				params |-> @Number(1)
			],
			// derivative of y by x is 0
			[
				params |-> fct(params[1]) == "Name",
				params |-> @Number(0)
			],
			// diff(f + g, x) = diff(f, x) + diff(g, x)
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "+",
				params |-> @Op("+", [ @Func("diff", [x, params[2]]) : x in args(params[1])[2] ])
			],
			// diff(f - g, x) = diff(f, x) - diff(g, x)
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "-",
				params |-> @Op("-", [ @Func("diff", [x, params[2]]) : x in args(params[1])[2] ])
			],
			// diff(x^n, x) = n * x^(n-1)
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "^",
				procedure(params) {
					sym := params[2];
					opTerm := params[1];
					base := args(opTerm)[2][1];
					exps := args(opTerm)[2][2..];
					if (base == sym) {
						// TODO:
						assert(#exps == 1 && fct(exps[1]) == "Number", "Differentiation of exponents that are more complex expressions than single numbers aren't implemented yet.");
						n := args(exps[1])[1];
						if (n == 0) {
							return @Func("diff", @Number(1));
						} else if (n == 1) {
							return @Func("diff", sym);
						} else {
							return @Op("*", [@Number(n), @Op("^", [sym, @Number(n-1)])]);
						}
					} else if (fct(base) == "Op" || fct(base) == "Func") {
						assert(false, "Differentiation of exponents of operator or function bases are not implemented yet.");
					} else {
						return @Number(0);
					}
				}
			],
			// diff(f * g, x) = diff(f, x) * g + f * diff(g, x)
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "*",
				procedure(params) {
					sym := params[2];
					opTerm := params[1];
					opArgs := args(opTerm)[2];
					inner := [ @Op("*", [@Func("diff", [opArgs[i], sym])] + [opArgs[j] : j in {1..#opArgs} | i != j]) : i in {1..#opArgs} ];
					return @Op("+", inner);
				}
			],
			// diff(f / g, x) = (diff(f, x) * g - f * diff(g, x)) / g^2
			[
				params |-> fct(params[1]) == "Op" && args(params[1])[1] == "/",
				procedure(params) {
					x := params[2];
					opTerm := params[1];
					opArgs := args(opTerm)[2];
					f := opArgs[1];
					g := opArgs[2];
					num := @Op("-", [@Op("*", [@Func("diff", [f, x]), g]), @Op("*", [f, @Func("diff", [g, x])])]);
					den := @Op("^", [g, @Number(2)]);
					return @Op("/", [num, den]);
				}
			],
			// diff(f(g(x)), x) = diff(g(x), x) * f(g(x))
			chainRule("diff", diffFuncRules)
		]],
		["sin", [
			// Must have exactly 1 argument
			checkArityRule("sin", 1),
			// Calculate numbers directly
			[
				params |-> fct(params[1]) == "Number",
				params |-> @Number(sin(params[1][1]))
			]
		]],
		["cos", [
			// Must have exactly 1 argument
			checkArityRule("cos", 1),
			// Calculate numbers directly
			[
				params |-> fct(params[1]) == "Number",
				params |-> @Number(cos(params[1][1]))
			]
		]]
	]);
};